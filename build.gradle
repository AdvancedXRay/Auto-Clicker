plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'maven-publish'
    id 'pro.mikey.plugins.insaniam' version "0.1-SNAPSHOT"
    id "me.modmuss50.mod-publish-plugin" version "0.4.5"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

def ENV = System.getenv()

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "fabricloader", project.loader_version
    inputs.property "mc_version", project.minecraft_version

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "fabricloader": project.loader_version,
                "mc_version": project.minecraft_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }
    repositories {
        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/releases"
                credentials {
                    username = "mikeymods"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}

def changelogData = insaniamUtils.createChangelog {
    file = file('CHANGELOG.md')
    versionPattern = ~/## \[[^]]+]/
    fallbackValue = "No changelog provided"
    version = project.mod_version
}

publishMods {
    def curseToken = providers.environmentVariable("CURSE_DEPLOY_TOKEN")

    dryRun = !curseToken.isPresent()
    changelog = changelogData
    version = project.mod_version
    type = STABLE
    file = remapJar.archiveFile

    curseforge {
        displayName = "Advanced XRay Fabric ${mod_version}"
        modLoaders.add("fabric")
        accessToken = curseToken
        projectId = project.curseforge_id
        minecraftVersions.add("${minecraft_version}")
        requires {
            slug = "fabric-api"
        }
    }

    modrinth {
        displayName = "Advanced XRay Fabric ${mod_version}"
        modLoaders.add("fabric")
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = project.modrinth_id
        minecraftVersions.add("${minecraft_version}")
        requires {
            slug = "fabric-api"
        }
    }

    github {
        repository = "AdvancedXRay/XRay-Fabric"
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        commitish = providers.environmentVariable("GITHUB_SHA").orElse("main")
        tagName = providers.environmentVariable("GITHUB_REF_NAME").orElse("v${mod_version}")
    }
}
